{
  description = "An empty Nix flake based environment";
  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-25.11";
    pre-commit-hooks = {
      url = "github:cachix/git-hooks.nix/master";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs =
    {
      self,
      nixpkgs,
      pre-commit-hooks,
    }@inputs:
    let
      supportedSystems = [
        "x86_64-linux"
        "aarch64-linux"
        "x86_64-darwin"
        "aarch64-darwin"
      ];
      forEachSupportedSystem =
        f: nixpkgs.lib.genAttrs supportedSystems (system: f { pkgs = import nixpkgs { inherit system; }; });
    in
    {
      checks = forEachSupportedSystem (
        { pkgs }:
        {
          pre-commit-check = inputs.pre-commit-hooks.lib.${pkgs.stdenv.hostPlatform.system}.run {
            src = ./.;
            hooks = {
              trufflehog = {
                enable = true;
                name = "ğŸ”’ Security Â· Detect hardcoded secrets";
              };
              nixfmt-rfc-style = {
                enable = true;
                name = "ğŸ” Code Quality Â· â„ï¸ Nix Â· Format";
                after = [ "trufflehog" ];
              };
              flake-checker = {
                enable = true;
                name = "âœ… Data & Config Validation Â· â„ï¸ Nix Â· Flake checker";
                args = [
                  "--check-supported"
                  "false"
                ];
                after = [ "nixfmt-rfc-style" ];
              };
              check-yaml = {
                enable = true;
                name = "âœ… Data & Config Validation Â· YAML Â· Lint";
                after = [ "nixfmt-rfc-style" ];
              };
              chktex = {
                enable = true;
                name = "ğŸ“ Docs Â· LaTex Â· Check";
                after = [
                  "flake-checker"
                  "check-yaml"
                ];
              };
              mdformat = {
                enable = true;
                name = "ğŸ“ Docs Â· Markdown Â· Format";
                after = [
                  "flake-checker"
                  "check-yaml"
                ];
              };
              just =
                let
                  package = pkgs.just;
                in
                {
                  enable = true;
                  package = package;
                  name = "ğŸ¤– Justfile Â· Format";
                  entry = "${package}/bin/just --fmt --unstable";
                  files = "^justfile$";
                  pass_filenames = false;
                  after = [
                    "mdformat"
                    "chktex"
                  ];
                };
              check-case-conflicts = {
                enable = true;
                name = "ğŸ“ Filesystem Â· Check case sensitivity";
                after = [ "just" ];
              };
              check-symlinks = {
                enable = true;
                name = "ğŸ“ Filesystem Â· Check symlinks";
                after = [ "just" ];
              };
              check-merge-conflicts = {
                enable = true;
                name = "ğŸŒ³ Git Quality Â· Detect conflict markers";
                after = [
                  "check-symlinks"
                  "check-case-conflicts"
                ];
              };
              forbid-new-submodules = {
                enable = true;
                name = "ğŸŒ³ Git Quality Â· Prevent submodule creation";
                after = [
                  "check-symlinks"
                  "check-case-conflicts"
                ];
              };
              no-commit-to-branch = {
                enable = true;
                name = "ğŸŒ³ Git Quality Â· Protect main branch";
                settings.branch = [ "main" ];
                stages = [ "pre-push" ];
                after = [
                  "check-symlinks"
                  "check-case-conflicts"
                ];
              };
              check-added-large-files = {
                enable = true;
                name = "ğŸŒ³ Git Quality Â· Block large file commits";
                args = [ "--maxkb=5000" ];
                after = [
                  "check-symlinks"
                  "check-case-conflicts"
                ];
              };
              commitizen = {
                enable = true;
                name = "ğŸŒ³ Git Quality Â· Validate commit message";
                stages = [ "commit-msg" ];
                after = [
                  "check-symlinks"
                  "check-case-conflicts"
                ];
              };
            };
          };
        }
      );
      devShells = forEachSupportedSystem (
        { pkgs }:
        let
          tex = (
            pkgs.texlive.combine {
              inherit (pkgs.texlive)
                scheme-small
                moderncv
                fontawesome5
                multirow
                arydshln
                ;
            }
          );
        in
        {
          default = pkgs.mkShell {
            inherit (self.checks.${pkgs.stdenv.hostPlatform.system}.pre-commit-check) shellHook;
            buildInputs = self.checks.${pkgs.stdenv.hostPlatform.system}.pre-commit-check.enabledPackages;
            packages =
              with pkgs;
              [
                just
                nil
                nixfmt-rfc-style
                texlivePackages.chktex
              ]
              ++ [ tex ];
            env = {
              shell = "zsh";
              NIL_PATH = "${pkgs.nil}/bin/nil";
            };
          };
        }
      );
    };
}
